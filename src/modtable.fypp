#:include "common.fypp"
module modtable
 use iso_fortran_env, only: int32, real32
 use modhash, only: roundinguppower2, seed_hash, hash
 implicit none
 private
 #:for t,k,l in TYPES
 public::table_${k}$_t
 #:endfor
 #:for t,k,l in NTYPES
 public::table_arr${k}$_t
 #:endfor

 integer(int32), parameter :: default_nel = 5000

 type, abstract :: tableabstract_t
  private
  integer(int32) :: filled
  integer(int32) :: nel
  integer(int32), allocatable :: id(:)
 contains
  private
  procedure(int_destroy_tableabstract_t), public, deferred :: destroy
  procedure :: destroy_tableabstract_t
  procedure, public :: getfilled => getfilled_tableabstract_t
  procedure, public :: getsize => getsize_tableabstract_t
 end type

 abstract interface
  pure subroutine int_destroy_tableabstract_t(this)
   import::tableabstract_t
   class(tableabstract_t), intent(inout) :: this
  end subroutine
 end interface

 #:for t,k,l in TYPES
 type, extends(tableabstract_t) :: table_${k}$_t !(k)
  private
  !integer, len :: k
  !character(len=k),allocatable::stored(:)
  ${t}$, allocatable ::stored(:)
 contains
  private
  procedure, public :: add => add_table${k}$_t
  procedure, public :: destroy => destroy_table${k}$_t
  procedure, public :: get => get_table${k}$_t
  procedure, public :: getindex => getindex_table${k}$_t
  procedure, public :: writetable => writetable_table${k}$_t
  final :: destroy_table${k}$_t_scal, destroy_table${k}$_t_array1
 end type

 interface table_${k}$_t
  module procedure constructor_table${k}$_t
 end interface

 #:endfor

 #:for t,k,l in NTYPES
 type, extends(tableabstract_t) :: table_arr${k}$_t
  private
  ${t}$, allocatable ::stored(:, :)
 contains
  private
  procedure, public :: add => add_table_arr${k}$_t
  procedure, public :: destroy => destroy_table_arr${k}$_t
  procedure, public :: get => get_table_arr${k}$_t
  procedure, public :: getindex => getindex_table_arr${k}$_t
  procedure, public :: writetable => writetable_table_arr${k}$_t
  final :: destroy_table_arr${k}$_t_scal, destroy_table_arr${k}$_t_array1
 end type

 interface table_arr${k}$_t
  module procedure constructor_table_arr${k}$_t
 end interface

 #:endfor

contains

 pure subroutine destroy_tableabstract_t(this)
  class(tableabstract_t), intent(inout) :: this

  this%filled = 0
  this%nel = 0
  if (allocated(this%id)) deallocate (this%id)

 end subroutine

 pure function getfilled_tableabstract_t(this) result(filled)
  class(tableabstract_t), intent(in) :: this
  integer(int32) :: filled

  filled = this%filled

 end function

 pure function getsize_tableabstract_t(this) result(nel)
  class(tableabstract_t), intent(in) :: this
  integer(int32) :: nel

  nel = this%nel

 end function

 #:for t,k,l in TYPES
  #:if k[0] == 'c'
 pure function constructor_table${k}$_t(k, nel) result(this)
  integer(kind=int32), intent(in) :: k
    #:else
  pure function constructor_table${k}$_t(nel) result(this)
     #:endif
   integer(kind=int32), intent(in), optional :: nel
   type(table_${k}$_t) :: this

   this%nel = roundinguppower2(default_nel)
   if (present(nel)) this%nel = roundinguppower2(nel)

   this%filled = 0

   allocate (this%id(this%nel))
   this%id = 0

     #:if k[0] == 'c'
   allocate (character(len=k) :: this%stored(this%nel))
   this%stored(:) = ''
     #:else
   allocate (this%stored(this%nel))
   this%stored = 0
     #:endif

  end function

  recursive subroutine add_table${k}$_t(this, c, index, lnew) !, col)
   class(table_${k}$_t), intent(inout) :: this
   ${l}$, intent(in) :: c
   integer(int32), intent(out), optional :: index
   !integer(int32), intent(out), optional :: col
   logical, intent(out), optional :: lnew

   integer(int32) :: i, address
   integer(int32), parameter :: maxiter = 5000

   if (real(this%filled) .gt. 0.80*this%nel) call increase_size()

   address = seed_hash - 1

   do i = 1, min(this%nel, maxiter) - 1
    address = iand(hash(c, address + i), this%nel - 1) + 1

    if (this%id(address) .eq. 0) then
     this%filled = this%filled + 1
     this%id(address) = this%filled
     this%stored(this%id(address)) = c
     if (present(index)) index = this%id(address)
     if (present(lnew)) lnew = .true.
     !if (present(col)) col = i
     return
    else !already occupied
     if (this%stored(this%id(address)) .eq. c) then
      if (present(index)) index = this%id(address)
      if (present(lnew)) lnew = .false.
      !if (present(col)) col = i
      return
     end if
    end if
   end do

   call increase_size()

   address = seed_hash - 1

   do i = 1, min(this%nel, maxiter) - 1
    address = iand(hash(c, address + i), this%nel - 1) + 1

    if (this%id(address) .eq. 0) then
     this%filled = this%filled + 1
     this%id(address) = this%filled
     this%stored(this%id(address)) = c
     if (present(index)) index = this%id(address)
     if (present(lnew)) lnew = .true.
     !if (present(col)) col = min(this%nel, maxiter) - 1 + i
     return
    else !already occupied
     if (this%stored(this%id(address)) .eq. c) then
      if (present(index)) index = this%id(address)
      if (present(lnew)) lnew = .false.
      !if (present(col)) col = min(this%nel, maxiter) - 1 + i
      return
     end if
    end if
   end do

   address = -1
   if (present(index)) index = -1

   error stop 'too many collisions'

    contains

     subroutine increase_size()
      type(table_${k}$_t) :: thistmp

      #:if k[0] == 'c'
       thistmp = table_${k}$_t(len(this%stored), nel=int(this%nel*1.5, int32))
      #:else
       thistmp = table_${k}$_t(nel=int(this%nel*1.5, int32))
      #:endif

      do i = 1, this%filled
       call thistmp%add(this%stored(i))
      end do

      this%filled = thistmp%filled
      this%nel = thistmp%nel
      if (allocated(this%id)) deallocate (this%id)
      if (allocated(this%stored)) deallocate (this%stored)
      call move_alloc(thistmp%id, this%id)
      call move_alloc(thistmp%stored, this%stored)

     end subroutine

  end subroutine

  pure subroutine destroy_table${k}$_t(this)
   class(table_${k}$_t), intent(inout) :: this

   call this%destroy_tableabstract_t

   if (allocated(this%stored)) deallocate (this%stored)

  end subroutine

  pure subroutine destroy_table${k}$_t_scal(this)
   type(table_${k}$_t), intent(inout) :: this

   call this%destroy()

  end subroutine

  pure subroutine destroy_table${k}$_t_array1(this)
   type(table_${k}$_t), intent(inout) :: this(:)

   integer :: i

   do i = 1, size(this)
    call this(i)%destroy()
   end do

  end subroutine

  pure function get_table${k}$_t(this, index) result(c)
   class(table_${k}$_t), intent(in) :: this
   integer(int32), intent(in) :: index
     #:if k[0] == 'c'
   character(len=len(this%stored)) :: c
     #:else
   ${t}$ :: c
     #:endif

   if (index .gt. this%filled) then
      #:if k[0] == 'c'
      #:else
    c = 0  !should NaN
      #:endif
    return !what should be the flag
   end if

   c = this%stored(index)

  end function

  pure function getindex_table${k}$_t(this, c) result(index)
   class(table_${k}$_t), intent(in) :: this
   ${l}$, intent(in) :: c
   integer(int32) :: index

   integer(int32) :: i, address
   integer(int32), parameter :: maxiter = 5000

   address = seed_hash - 1

   do i = 1, min(this%nel, maxiter) - 1
    address = iand(hash(c, address + i), this%nel - 1) + 1

    if (this%id(address) .eq. 0) then
     index = -2
     return
    else !occupied
     if (this%stored(this%id(address)) .eq. c) then
      index = this%id(address)
      return
     end if
    end if
   end do

   index = -1

  end function

  subroutine writetable_table${k}$_t(this, namefile)
   class(table_${k}$_t), intent(in) :: this
   character(*), intent(in) :: namefile

   integer :: i, un

   open (newunit=un, file=namefile, action='write')
   do i = 1, this%filled
    write (un, '(i10,1x,g0)') i, this%get(i)
   end do
   close (un)

  end subroutine

    #:endfor

    #:for t,k,l in NTYPES
  pure function constructor_table_arr${k}$_t(k, nel) result(this)
   integer(kind=int32), intent(in) :: k
   integer(kind=int32), intent(in), optional :: nel
   type(table_arr${k}$_t) :: this

   this%nel = roundinguppower2(default_nel)
   if (present(nel)) this%nel = roundinguppower2(nel)

   this%filled = 0

   allocate (this%id(this%nel))
   this%id = 0

   allocate (this%stored(k, this%nel))
   this%stored = 0

  end function

  recursive subroutine add_table_arr${k}$_t(this, c, index, lnew)
   class(table_arr${k}$_t), intent(inout) :: this
   ${l}$, intent(in) :: c(:)
   integer(int32), intent(out), optional :: index
   logical, intent(out), optional :: lnew

   integer(int32) :: i, address
   integer(int32), parameter :: maxiter = 5000

   if (size(c) .ne. size(this%stored, 1)) then
    if (present(index)) index = -1
    if (present(lnew)) lnew = .false.
    return
   end if

   if (real(this%filled) .gt. 0.80*this%nel) call increase_size()

   address = seed_hash - 1

   do i = 1, min(this%nel, maxiter) - 1
    address = iand(hash(c, address + i), this%nel - 1) + 1

    if (this%id(address) .eq. 0) then
     this%filled = this%filled + 1
     this%id(address) = this%filled
     this%stored(:, this%id(address)) = c
     if (present(index)) index = this%id(address)
     if (present(lnew)) lnew = .true.
     return
    else !already occupied
     if (all(this%stored(:, this%id(address)) .eq. c)) then
      if (present(index)) index = this%id(address)
      if (present(lnew)) lnew = .false.
      return
     end if
    end if
   end do

   call increase_size()

   address = seed_hash - 1

   do i = 1, min(this%nel, maxiter) - 1
    address = iand(hash(c, address + i), this%nel - 1) + 1

    if (this%id(address) .eq. 0) then
     this%filled = this%filled + 1
     this%id(address) = this%filled
     this%stored(:, this%id(address)) = c
     if (present(index)) index = this%id(address)
     if (present(lnew)) lnew = .true.
     return
    else !already occupied
     if (all(this%stored(:, this%id(address)) .eq. c)) then
      if (present(index)) index = this%id(address)
      if (present(lnew)) lnew = .false.
      return
     end if
    end if
   end do

   address = -1
   if (present(index)) index = -1

   error stop 'too many collisions'

  contains

   subroutine increase_size()
    type(table_arr${k}$_t) :: thistmp

    thistmp = table_arr${k}$_t(size(this%stored, 1), nel=int(this%nel*1.5, int32))

    do i = 1, this%filled
     call thistmp%add(this%stored(:, i))
    end do

    this%filled = thistmp%filled
    this%nel = thistmp%nel
    if (allocated(this%id)) deallocate (this%id)
    if (allocated(this%stored)) deallocate (this%stored)
    call move_alloc(thistmp%id, this%id)
    call move_alloc(thistmp%stored, this%stored)

   end subroutine

  end subroutine

  pure subroutine destroy_table_arr${k}$_t(this)
   class(table_arr${k}$_t), intent(inout) :: this

   call this%destroy_tableabstract_t

   if (allocated(this%stored)) deallocate (this%stored)

  end subroutine

  pure subroutine destroy_table_arr${k}$_t_scal(this)
   type(table_arr${k}$_t), intent(inout) :: this

   call this%destroy()

  end subroutine

  pure subroutine destroy_table_arr${k}$_t_array1(this)
   type(table_arr${k}$_t), intent(inout) :: this(:)

   integer :: i

   do i = 1, size(this)
    call this(i)%destroy()
   end do

  end subroutine

  pure function get_table_arr${k}$_t(this, index) result(c)
   class(table_arr${k}$_t), intent(in) :: this
   integer(int32), intent(in) :: index
   ${t}$ :: c(size(this%stored, 1))

   if (index .gt. this%filled) then
    c = 0
    return !what should be the flag
   end if

   c = this%stored(:, index)

  end function

  pure function getindex_table_arr${k}$_t(this, c) result(index)
   class(table_arr${k}$_t), intent(in) :: this
   ${l}$, intent(in) :: c(:)
   integer(int32) :: index

   integer(int32) :: i, address
   integer(int32), parameter :: maxiter = 5000

   if (size(c) .ne. size(this%stored, 1)) then
    index = -1
    return
   end if

   address = seed_hash - 1

   do i = 1, min(this%nel, maxiter) - 1
    address = iand(hash(c, address + i), this%nel - 1) + 1

    if (this%id(address) .eq. 0) then
     index = -1
     return
    else !occupied
     if (all(this%stored(:, this%id(address)) .eq. c)) then
      index = this%id(address)
      return
     end if
    end if
   end do

   index = -1

  end function

  subroutine writetable_table_arr${k}$_t(this, namefile)
   class(table_arr${k}$_t), intent(in) :: this
   character(*), intent(in) :: namefile

   integer :: i, un

   open (newunit=un, file=namefile, action='write')
   do i = 1, this%filled
    write (un, '(i10,*(1x,g0))') i, this%get(i)
   end do
   close (un)

  end subroutine

    #:endfor

  end module
